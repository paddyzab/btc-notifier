apply plugin: 'com.android.application'

def quotate(text) {
    return "\"" + text + "\""
}

def getVersionCode = { ->
    def code = project.hasProperty('versionCode') ? versionCode.toInteger() : 1
    return code
}

android {
    compileSdkVersion 20
    buildToolsVersion '20.0.0'

    defaultConfig {
        applicationId "com.paddy.btc.notifier.btc_notifier"
        minSdkVersion 15
        targetSdkVersion 20
        versionCode getVersionCode();
        versionName "1.0"
    }

    signingConfigs {
        release {
            storeFile file("signing/release.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        develop {
            storeFile file("develop.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        def STRING = "String"
        def BOOLEAN = "boolean"
        def TRUE = "true"
        def FALSE = "false"

        def API_LOG_LEVEL = "API_LOG_LEVEL"
        def CONSUMER_API_BASE_URL = "API_BASE_URL"

        release {
            signingConfig signingConfigs.release
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField STRING, API_LOG_LEVEL, quotate("NONE")
            buildConfigField STRING, CONSUMER_API_BASE_URL, quotate("https://api.coindesk.com/v1/bpi/")
        }

        debug {
            signingConfig signingConfigs.develop
            runProguard false
            applicationIdSuffix '.debug'
            buildConfigField STRING, API_LOG_LEVEL, quotate("FULL")
            buildConfigField STRING, CONSUMER_API_BASE_URL, quotate("https://api.coindesk.com/v1/bpi/")
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.squareup.retrofit:retrofit:1.7.0'
    compile 'com.jakewharton:butterknife:5.1.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.1'
    compile 'com.google.guava:guava:18.0'
    compile 'joda-time:joda-time:2.3'
    compile 'com.netflix.rxjava:rxjava-android:0.20.4'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'commons-lang:commons-lang:2.3'

    // test dependencies
    androidTestCompile 'junit:junit:4.11'
    androidTestCompile "org.mockito:mockito-core:1.+"
    androidTestCompile "org.assertj:assertj-core:1.0.0"

    compile fileTree(dir: 'libs', include: ['*.jar'])
}
